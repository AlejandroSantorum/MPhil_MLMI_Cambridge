#!/bin/csh -f

#set verbose

# This shell script invokes HHEd to perform a global edit of a set
# of models.  It must be executed from within the directory holding
# the hmm directories.
#
# Usage:   hedit HTE sourcedir targetdir hedfile
#
#          HTE file contains the config variables
#          sourcedir/MMF is the input HMM file
#          targetdir/MMF is the output HMM file
#          hedfile is the HHEd edit config file
#
# valid HTE config variables:
#
#       HMMLIST  - the HMM list file
#       HHTRACE  - the trace level of HHEd
#       HECONFIG - the -C config file for HHEd
#       HHBIN    - the HHEd binary to invoke
#
# Original Copyright (c) Steve Young 1992, 1993
#
# Last Updated 12/10/2015 by cz277@cam.ac.uk
#

set HMMLIST
set HHBIN = tools/htkbin/HHEd

if ( $#argv != 4 ) then
   echo "hedit HTE source target hedfile"
   exit 0
endif

set HTE = $argv[1]

if ( -d $argv[2] ) then
   set src = (-H $argv[2]/MMF)
else
   echo "No source dir $argv[2]"
   exit 1
endif
source $HTE

set tgt = (-M $argv[3])

if ( ! -d $argv[3] ) mkdir $argv[3]

if ( ! -f $HMMLIST ) then
   echo "List of models to edit $HMMLIST does not exist"
   exit 1
endif

if ( ! -f $argv[4] ) then
   echo "Edit command file $argv[4] does not exist"
   exit 1
endif

if ($?HHTRACE) then
   set OPT = (-T $HHTRACE)
else
   set OPT = (-T 1)
endif

if ($?HECONFIG) then
    set OPT = ($OPT -C $HECONFIG) 
endif

${HHBIN} -A -D -V $OPT $src $tgt $argv[4] $HMMLIST > $argv[3]/LOG

